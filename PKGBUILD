# Maintainer: Max Pray a.k.a. Synthead <synthead@gmail.com>

pkgname="kernel26-crusoe"
pkgdesc="The Linux Kernel and modules (for Transmeta Crusoe CPUs)"
url="http://www.kernel.org"
arch=('i686')
license=('GPL2')
pkgver=2.6.37.4
pkgrel=1
groups=('base')
depends=('coreutils' 'linux-firmware' 'module-init-tools' 'mkinitcpio>=0.5.20')
optdepends=('crda: to set the correct wireless channels of your country')
replaces=('kernel24' 'kernel24-scsi' 'kernel26-scsi' 'alsa-driver' 'ieee80211'
          'hostap-driver26' 'pwc' 'nforce' 'squashfs' 'unionfs' 'ivtv' 'zd1211'
          'kvm-modules' 'iwlwifi' 'rt2x00-cvs' 'gspcav1' 'atl2' 'wlan-ng26'
          'rt2500' 'nouveau-drm')
provides=('kernel26')
backup=("etc/mkinitcpio.d/$pkgname.preset")
source=("ftp://ftp.kernel.org/pub/linux/kernel/v2.6/linux-${pkgver%.*}.tar.bz2"
        "ftp://ftp.archlinux.org/other/kernel26/patch-$pkgver-1-ARCH.bz2"
        'config.crusoe'
        "$pkgname.preset"
	"crusoe-nopl-emu-$pkgver.patch")
md5sums=('c8ee37b4fdccdb651e0603d35350b434'
         'b3ee14c119e9f65ecd699d486a8372c9'
         'dc6660b98a267c3fb164d2556d911724'
         '8181d56c218e102843b61f1b0431f52a'
         '114204bb5a73aaad77cc1c4f2f5e67fc')
install=$pkgname.install

build() {
 cd "$srcdir/linux-${pkgver%.*}"
 patch -Np1 < "$srcdir/patch-$pkgver-1-ARCH"
 patch -Np1 < "$srcdir/crusoe-nopl-emu-$pkgver.patch"

 cp "$srcdir/config.crusoe" .config
 make prepare
 make bzImage modules
}

package () {
 cd "$srcdir/linux-${pkgver%.*}"
 mkdir -p "$pkgdir"/{lib/modules,boot}
 make INSTALL_MOD_PATH="$pkgdir" modules_install

 _kernver=${pkgver%.*}${pkgname#kernel26}
 cp System.map "$pkgdir/boot/System.map26${pkgname#kernel26}"
 cp arch/x86/boot/bzImage "$pkgdir/boot/vmlinuz26${pkgname#kernel26}"
 install -Dm644 vmlinux "$pkgdir/usr/src/linux-$_kernver/vmlinux"
 install -Dm644 "$srcdir/$pkgname.preset" "$pkgdir/etc/mkinitcpio.d/$pkgname.preset"
 sed -i "s/\(KERNEL_VERSION=\).*/\1$_kernver/g" "$startdir/$pkgname.install"
 cat <<< $'# DO NOT EDIT THIS FILE\nALL_kver='"$_kernver" > "$pkgdir/etc/mkinitcpio.d/$pkgname.kver" 

 rm -rf "$pkgdir/lib"/{modules/$_kernver/{source,build},firmware}
}
